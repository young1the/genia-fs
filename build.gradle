plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.4'
    id 'io.spring.dependency-management' version '1.1.3'
    id "com.github.node-gradle.node" version "7.0.1"
}

group = 'com.chunjae'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.cloud:spring-cloud-starter-aws:2.2.6.RELEASE'
    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-webflux', version: '3.1.5'
    testImplementation 'junit:junit:4.4'

    implementation group: 'org.apache.poi', name: 'poi', version: '5.2.4'
    implementation group: 'org.apache.poi', name: 'poi-ooxml', version: '5.2.4'

// QueryDSL --- START
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor "com.querydsl:querydsl-apt:5.0.0:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"
}
def querydslDir = "$buildDir/generated/querydsl"

sourceSets {
    main.java.srcDirs += [ querydslDir ]
}

tasks.withType(JavaCompile) {
    options.annotationProcessorGeneratedSourcesDirectory = file(querydslDir)
}

clean.doLast {
    file(querydslDir).deleteDir()
}
// QueryDSL --- END

// NODE -- START
node {
    download = true
    nodeProjectDir = file("${project.projectDir}/react")
}

task npmBuild(type: NpmTask, dependsOn: npmInstall) {
    args = ['run','build']
}

task copyDist(type: Copy, dependsOn: npmBuild) {
    from 'react/dist'
    into 'build/resources/main/static/react'
}

processResources.dependsOn 'copyDist'
// NODE -- END

tasks.named('test') {
    useJUnitPlatform()
}